/*
 * MIT License
 *
 * Copyright (c) 2018 Jan Heinrich Reimer
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

// Android app plugin
apply plugin: "com.android.application"
// Kotlin plugin
apply plugin: "kotlin-android"
// Kotlin convenience extensions for Android plugin
apply plugin: "kotlin-android-extensions"
// Kotlin annotation processor plugin
apply plugin: "kotlin-kapt"

android {

    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "rocks.poopjournal.metadataremover"

        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode rootProject.appVersionMajor * 100000 +
                rootProject.appVersionMinor * 10000 +
                rootProject.appVersionPatch * 1000 +
                rootProject.appVersionBuild
        versionName "${rootProject.appVersionMajor}.${rootProject.appVersionMinor}" +
                (rootProject.appVersionPatch != 0 ? ".${rootProject.appVersionPatch}" : "")

        // The default test runner for Android instrumentation tests.
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        // Debug builds
        debug {
            // Append "DEBUG" to all debug build versions
            versionNameSuffix ' DEBUG'
        }

        // Production builds
        release {
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
                proguardFile 'proguard-rules.pro'
            }
        }
    }

    // Enable Android data binding.
    dataBinding {
        enabled = true
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events "passed", "skipped", "failed", "standardOut", "standardError"
        }
    }

    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.kotlinPluginVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$rootProject.kotlinPluginVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.27.0-eap13"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.27.0-eap13"

    // Material Design
    implementation "com.google.android.material:material:1.0.0"

    // AppCompat support
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.core:core-ktx:1.0.1"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.exifinterface:exifinterface:1.0.0"
    // Architecture components
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"

    // Timber logging util
    implementation "com.jakewharton.timber:timber:4.6.0"

    // Local unit test libraries
    testImplementation "junit:junit:4.12"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$rootProject.kotlinPluginVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$rootProject.kotlinPluginVersion"
    androidTestImplementation "androidx.test:runner:1.1.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.0"

    // I/O
    implementation "com.squareup.okio:okio:1.14.0"
    implementation "com.github.bumptech.glide:glide:4.8.0"
    //implementation "org.apache.commons:commons-imaging:1.0-R1401825"
    //implementation "org.apache.tika:tika-core:1.17"
    //implementation "org.apache.tika:tika-parsers:1.17"
    //implementation "com.drewnoakes:metadata-extractor:2.11.0"

    //implementation "com.getkeepsafe.taptargetview:taptargetview:1.9.1"
    implementation "com.sothree.slidinguppanel:library:3.4.0"
    implementation "com.github.zagum:Android-ExpandIcon:1.2.1"
    implementation "com.mikepenz:aboutlibraries:6.2.0"
}
